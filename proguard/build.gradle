buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://maven.pkg.jetbrains.space/public/p/ktor/eap"
        }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"
        classpath "com.guardsquare:proguard-gradle:7.1.0"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.0.0"
    }
}

apply plugin: 'kotlin'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'application'

mainClassName = "io.ktor.server.netty.DevelopmentEngine"

sourceSets {
    main.kotlin.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.pkg.jetbrains.space/public/p/ktor/eap"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.31"
    implementation "io.ktor:ktor-server-default-headers:2.0.0-eap-256"
    implementation "io.ktor:ktor-server-call-logging:2.0.0-eap-256"
    implementation("io.ktor:ktor-server-netty:2.0.0-eap-256")
    implementation("io.ktor:ktor-html-builder:2.0.0-eap-256")
    implementation "ch.qos.logback:logback-classic:1.2.6"
}

shadowJar {
    baseName = 'my-application'
    classifier = null
    version = null
}

task minimizedJar(type: proguard.gradle.ProGuardTask, dependsOn: shadowJar) {
    injars "build/libs/my-application.jar"
    outjars "build/libs/my-application.min.jar"

    if (System.getProperty('java.version').startsWith('1.')) {
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {
        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
    }

    printmapping "build/libs/my-application.map"
    ignorewarnings
    dontobfuscate
    dontoptimize
    dontwarn
    //optimizations '!code/allocation/variable'

    def keepClasses = [
            'io.ktor.server.netty.DevelopmentEngine',
            'kotlin.reflect.jvm.internal.**',
            'io.ktor.samples.hello.HelloApplicationKt',
            'kotlin.text.RegexOption'
    ]

    for (keepClass in keepClasses) {
        keep access: 'public', name: keepClass, {
            method access: 'public'
            method access: 'private'
        }
    }
}
