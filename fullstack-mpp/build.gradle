buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.jetbrains.space/public/p/ktor/eap"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"
    }
}

apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        js('frontend') {
            browser {
                testTask {
                    // TODO: disable browser tests since we can't run it on teamcity agents yet
                    enabled = false
                }
            }
        }
        jvm('backend') {
        }
    }

    sourceSets {
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
                implementation "org.jetbrains.kotlin:kotlin-test-common"
            }
        }
        backendMain {
            dependencies {
                implementation "io.ktor:ktor-server-netty:2.0.0-eap-256"
                implementation "io.ktor:ktor-html-builder:2.0.0-eap-256"
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
                implementation "ch.qos.logback:logback-classic:1.2.6"
            }
        }
        backendTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test"
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
        frontendMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
                implementation "org.jetbrains.kotlin:kotlin-test-js"
                implementation "io.ktor:ktor-client-core:2.0.0-eap-256"
                implementation "io.ktor:ktor-client-js:2.0.0-eap-256"
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
    maven { url "https://maven.pkg.jetbrains.space/public/p/ktor/eap" }

}

tasks.getByName('frontendBrowserProductionWebpack') {
    outputFileName = 'output.js'
}

tasks.getByName('backendJar') {
    dependsOn(tasks.getByName('frontendBrowserProductionWebpack'))
    def frontendBrowserProductionWebpack = tasks.getByName('frontendBrowserProductionWebpack')
    from(new File(frontendBrowserProductionWebpack.destinationDirectory, frontendBrowserProductionWebpack.outputFileName))
}

task run(type: JavaExec, dependsOn: [backendJar]) {
    main = "io.ktor.samples.fullstack.backend.BackendCodeKt"
    classpath(configurations.backendRuntimeClasspath, backendJar)
    args = []
}
